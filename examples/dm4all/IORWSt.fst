module IORWSt

(* Reasoning about IO, considering both inputs and outputs. With access
 * to the history of previous events and postcondition over the events
 * generated by the program, in update monad style. *)

type input = int
type output = int

noeq
type io a =
  | Read    : (input -> io a) -> io a
  | Write   : output -> io a -> io a
  | Return  : a -> io a

let return (a:Type u#a) (x:a) = Return x

let rec bind (a : Type u#aa) (b : Type u#bb)
         (l : io a) (k : a -> io b) : io b =
  match l with
  | Read f -> Read (fun i -> FStar.WellFounded.axiom1 f i; bind _ _ (f i) k)
  | Write o k' -> Write o (bind _ _ k' k)
  | Return v -> k v

type event =
  | In  : input -> event
  | Out : output -> event

let trace = list event

let post a = a -> trace -> Type0
let wpty a = trace -> post a -> Type0

unfold
let return_wp (a:Type) (x:a) : wpty a =
  fun h p -> p x h

unfold
let bind_wp (_ : range) (a:Type) (b:Type) (w : wpty a) (kw : a -> wpty b) : wpty b =
  fun h p -> w h (fun x h' -> kw x h' (fun y h'' -> p y h''))

let rec interpretation #a (m : io a) (h : trace) (p : post a) : Type0 =
  match m with
  | Write o m -> interpretation m ((Out o)::h) p
  | Read f -> forall (i : input). (FStar.WellFounded.axiom1 f i; interpretation (f i) ((In i)::h) p)
  | Return x -> p x h

total
reifiable
reflectable
new_effect {
  IO : a:Type -> Effect
  with
       repr      = io
     ; return    = return
     ; bind      = bind

     ; wp_type   = wpty
     ; return_wp = return_wp
     ; bind_wp   = bind_wp

     ; interp = interpretation
}

val read : unit -> IO int (fun h p -> forall x. p x ((In x)::h))
let read () =
    IO?.reflect (Read (fun i -> Return i))

val write : o:int -> IO unit (fun h p -> p () ((Out o)::h))
let write i =
    IO?.reflect (Write i (Return ()))

let test1 () : IO int (fun h p -> p 1 ((Out 3)::(Out 2)::h)) =
  write 2;
  write 3;
  1

let test2 () : IO int (fun h p -> forall i . p 1 ([Out 3; In i; Out 2]@h)) =
  write 2;
  let x = read () in
  write 3;
  1

effect Io (a:Type) (pre':trace -> Type0) (post':trace -> a -> trace -> Type0) =
        IO a (fun h p -> pre' h /\ (forall r l . post' h r l ==> p r l))

let test3 (i:int)
  : Io int (requires (fun h     -> exists h' h'' . h = h' @ (In i :: h'')))
           (ensures  (fun h x l -> exists x . l = [Out x; In x; Out i]@h)) =
  write i;
  let x = read () in
  write x;
  1

let rec n_w_events (n:nat) (i:int) =
  if n = 0
  then []
  else Out i :: n_w_events (n - 1) i

let rec test4 (n:nat) (i:int)
  : Io unit (requires (fun _     -> True))
            (ensures  (fun h x l -> l = (n_w_events n i)@h)) =
  if n = 0
  then ()
  else (test4 (n - 1) i; write i)
